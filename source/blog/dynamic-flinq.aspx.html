<!-- [info]

 - date: 2007-08-18T02:38:11.0000000
 - description: In this article I will introduce FLinq, the F# support for language integrated query and show how we can construct a database queries dynamically in a type-safe way in F#.
 - tags: c#,meta-programming,f#
 - layout: article
 - title: Building LINQ Queries at Runtime in F#
 - url: dynamic-flinq.aspx

[/info] -->
<!-- [abstract]
<p>In an article about building LINQ queries at runtime in C# 3.0, I described how you can build
  a LINQ query dynamically, for example by combining a set of conditions using the 'or' operator in the
  where clause. I mentioned that the way I implemented it is largely influenced by the F# language, 
  which provides very natural way for manipulations with code like this. In this article I will first 
  shortly introduce FLINQ sample, which is an F# library implementing LINQ support and than I will 
  implement the same examples I presented in the earlier article in F#.</p>
[/abstract] -->

<h1>Building LINQ Queries at Runtime in F#</h1>
<p>In an article about building LINQ queries at runtime in C# 3.0 [<a href="#dynamicflinq">1</a>], I described how you can build
  a LINQ query dynamically, for example by combining a set of conditions using the 'or' operator in the
  where clause. I mentioned that the way I implemented it is largely influenced by the F# language, 
  which provides very natural way for manipulations with code like this. In this article I will first 
  shortly introduce FLINQ sample, which is an F# library implementing LINQ support and than I will 
  implement the same examples I presented in the earlier article in F#.</p>

<h2>F# Comprehensions and Quotations</h2>
<p>First, it is interesting to note that in F#, the language integrated query (LINQ) is not directly
  supported in the F# language, but instead is supported as a library. The code that you can use for
  writing queries is written using <em>comprehensions</em> which is a very common language construct
  in functional languages. In F# you can use it for example for manipulating with list of numbers:
</p>
<pre lang="fsharp">
let evenSquares = 
 [ for n in [0 .. 10] 
   when n%2 = 0
   -&gt; n*n ]
   
// evenSquares : int list 
//   [0; 4; 16; 25; 36; 64; 100]
</pre>
<p>Here, we use the comprehension to build a list of integers. When building the list, we first take
  all numbers between 0 and 10, filter only even integers (in the <code>when</code> clause, which
  does filtering) and than return a square of every number (in the <code>-&gt;</code> section, which
  performs projection). We can see that this is very similar to a syntax used in C#, which has
  <code>from .. in</code> keywords to define the source of data, <code>where</code> to define filtering
  and finally, <code>select</code> to define projection. In F# however, the syntax is not tightly 
  tied with the LINQ and can be used for different purposes as well. </p>
<p>As you probably know, the second important feature that enables LINQ are <em>expression trees</em>.
  In C# this means that the program can get a representation of an anonymous function and manipulate with 
  it - for example translate it to SQL. The C# compiler determines whether an anonymous function should
  be compiled as a delegate or as an expression tree by the type of a variable. If you use <code>Func&lt;..&gt;</code>,
  the function is compiled as a delegate and if you use <code>Expression&lt;Func&lt;...&gt;&gt;</code>, you
  will get an expression tree.</p>  
<p>On the other side, in F# this can be done explicitly by marking any block of code using quotation
  symbols. A code marked using these symbols will be compiled as an F# <em>quotation</em>, which is a representation
  of F# code, similar as the C# expression tree. The reason why F# doesn't use the same representation 
  as C# is that in F# you can quote more complicated code and not just a simple expression.
  The following code gives a few examples in both F# and C# (when possible):</p>  
<pre lang="fsharp">
// 'add' as a function &amp; an expression in C#
Func&lt;int,int,int&gt;             addF = (a,b) =&gt; a + b
Expression&lt;Func&lt;int,int,int&gt;&gt; addE = (a,b) =&gt; a + b

// 'add' as a function &amp; a quotation in F#
let addF =   fun a b -> a + b
let addE = « fun a b -> a + b »

// More complex quotation in F#
let addAndPrint = 
  « fun a b -> 
      let sum = a + b
      printf "Sum = %d" sum »
</pre>
<p>Note that if you don't want to use Unicode symbols (<code>«</code>, <code>»</code>) in your code you can use <code>&lt;@@</code> and <code>@@&gt;</code>
  that serve exactly for the same purpose. I'll use the Unicode symbols, because I believe that it looks
  more readable and once you define a few macros in Visual Studio it is easy to write them.
  As we can see, thanks to the fact that in F# it is explicitly specified what code is a quotation and
  what is an ordinary code, you don't have to write the types, because the F# type inference algorithm
  is able to deduce the types for us. The type of <code>addF</code> is inferred as <code>int -&gt; int -&gt; int</code>
  (function taking two integers and returning an integer) and the type of <code>addE</code> is 
  <code>Expr&lt;int -&gt; int -&gt; int&gt;</code>, which is a quotation of the function. The second
  difference is that in F# you can quote more complex code as demonstrated by the <code>addAndPrint</code> example.
  This isn't important for supporting FLINQ, but it is very useful - for example it allows translating F# code to
  JavaScript which is implemented in my project F# WebToolkit [<a href="#dynamicflinq">2</a>].</p>
  
<h2>FLINQ Introduction</h2>
<p>Let's now look how the two F# features that I mentioned can be combined together to build an F# language
  support for querying database. The FLINQ is distributed as an F# example and you can find it in the 
  <code>samples/fsharp/FLinq</code> directory (in the F# installation folder). The version available with 
  F# 1.9.2.9 (currently the latest release) is compatible with VS 2008 beta 1, but I built a version compatible 
  with beta 2 as well, which you can get below. If you open the <code>dlinqsamples.fs</code> file, you can
  see a lot of F# samples for querying the Northwind database, so let me explain a few of them:</p>
<pre lang="fsharp">
// Just select all customers
let sample1() =
  SQL « { for c in (§db.Customers) -&gt; c } »
  |&gt; dump 

// Select all customers from UK
let sample2() =
  SQL « { for c in (§db.Customers) 
          when c.Country = "UK"
          -&gt; c } »
  |&gt; dump 
    
// Join products &amp; categories using nested 'for'    
let sample3() =
  SQL « { for p in (§db.Products) 
          for c in (§db.Categories)
          when c.CategoryID = p.CategoryID
          -&gt; (c.CategoryName, p.ProductName) } »     
  |&gt; dump
</pre>  
<p>As we can see in all three examples, FLINQ wraps the whole comprehension (that specifies what we
  want to select from a database) in a quotation, so the F# code can be translated to SQL and executed.
  The translation and execution is done by the <code>SQL</code> function, which actually uses 
  LINQ from the .NET 3.5 internally. In all three examples we use a <code>dump</code> function (which
  is implemented in the FLINQ sample) to print the results, which are returned as a sequence of
  tuple containing a name of category and product names - the type of the result is <code>seq&lt;string * string&gt;</code>.  
  We could as well use a <code>for</code> loop or for example <code>Seq.iter</code> function which executes 
  a given function for every element in the sequence.</p>
<p>So far I didn't explain the <code>§</code> symbol - it can be used in a quotation to embed a 
  value into the tree. This means that when you're building a query that uses some value that is
  entered by the user (for example a name of the country for filtering), you can embed the value
  into a query using this symbol, without this symbol, the quotation will contain just a reference to
  a variable name, which isn't what we want in this situation. So, in all three samples we use the <code>§</code>
  symbol for embedding the value representing a database table in the quotation, so the translator can
  use it for querying the database.</p>  
<p>For writing the FLINQ queries, we can use similar comprehensions as we used earlier. For example
  when selecting all customers living in UK in the second sample, we use a <code>when</code>
  to write a filter that selects only customers we want and we also use <code>-&gt;</code>
  for projection - in the third example we want to select only a category name and a product name.
  In the third example we return an F# tuple (which is just a pair of two values) in the projection.
  This is a very compact way for writing the code - in contrast to C# 3.0 (where similar thing is achieved
  using anonymous types) in F# it didn't require any language change.</p>

<h2>Dynamic Queries #1: Selecting Customers</h2>
<p>Now we can finally look at everything you need to know from F# to understand how to build
  LINQ-like queries dynamically in F#. Let's start with a same example as in the previous article, we will 
  build a query for selecting customers from the Northwind database and we will let the user to choose what property of 
  the <code>Customer</code> type he wants to enter (a choice between <code>CompanyName</code>, <code>Country</code> and a 
  <code>ContactName</code>). After choosing the property, we will ask for the value and run the query 
  constructed using the given values. First, we will write a utility function for printing a sequence of customers
  (which is what the <code>SQL</code> function returns) and create a Northwind data context (similarly as in 
  C# example):</p>  
<pre lang="fsharp">
/// Utility function to dump a sequence (IEnumerable) of customers
let dump (q:#seq&lt;Customer&gt;) = 
  for c in q do
    Console.WriteLine
      ("{0,-25}{2,10}, {1,-8}{3,-30}", 
       c.CompanyName, c.Country, c.City, c.ContactName)
  Console.ReadLine();

/// Northwind data context and customers table
let db = new Northwind("Server=.; Database=Northwind; Integrated Security=SSPI")
let custTable = « §db.Customers »
</pre>
<p>At the last line of the sample, we created a value <code>custTable</code> which is an F# quotation containing
  just a value of the customers data table - we will need it later in the examples. Now, let's build a function
  for building the query. The function takes two arguments, the first arguments is a quotation of a function
  that selects a property of the <code>Customer</code> type and the second argument is a quotation of a value
  that we want to look for:</p>
<pre lang="fsharp">
/// Function for building a query working with the customer table.
/// Selector is of type 'CustomerPropSelector'
let queryTemplate selector (value:Expr&lt;string&gt;) = 
  custTable |&gt; 
    « { for c in _ do
          let s = (_ c) in
          when (s:string).IndexOf(_:string) &lt;&gt; -1
          -&gt; c } » value selector
</pre>
<p>There is a one new interesting thing in this example, we used underscore at a few places in the quotation.
  The underscore in the quotation means that we want to build a <em>quotation template</em>, which is a quotation
  with placeholders that can be filled later. Technically, this means that we build a function that produces
  a quotation, once you give it all the required arguments to fill the holes. In this code, we use three underscores
  and we give it three arguments: <code>value</code>, <code>selector</code> and also <code>custTable</code>
  (which is written before the query using the pipe-lining operator to get a good type inference). We use a
  few type-annotations in the query, so the compiler knows what the types of the <em>holes</em> are, for example that 
  the <code>selector</code> returns a <code>string</code> (which has the <code>IndexOf</code> method). The 
  <code>queryTemplate</code> function can be used rather easily:</p>
<pre lang="fsharp">
let q = SQL (queryTemplate « fun c -> c.CompanyName » « "ab" »)
q |> dump
</pre>
<p>As mentioned, both parameters are quotations (so that they can be used as an arguments for the quotation
  template that we wrote earlier), which means that we have to give it two quoted blocks of F# code.
  In our case, the first argument is a quoted function that returns a <code>CompanyName</code> property
  of a customer and the second argument is a quoted string literal.</p>
<p>In the next code snippet we finish the example and add the interactive part that allows
  selecting a property of the customer and entering the required value:</p>
<pre lang="fsharp">
/// Dictionary for getting a 'selector' of a Customer property
/// with key of type string
let (dict:Map&lt;string,CustomerPropSelector&gt;) = 
  Map.of_list
    [ "CompanyName", « fun (c:Customer) -&gt; c.CompanyName »;
      "Country",     « fun (c:Customer) -&gt; c.Country »;
      "ContactName", « fun (c:Customer) -&gt; c.ContactName »; ]
      
/// Demonstrates the use of 'queryTemplate' function
let main() =
  Console.Write("Filter - field name (CompanyName, ContactName, Country):\n&gt; ");
  let prop = Console.ReadLine();
  Console.Write("Filter - value:\n&gt; ");
  let value = Console.ReadLine();
  
  let q = queryTemplate dict.[prop] « §value »
  SQL q |&gt; dump
</pre>
<p>Similarly as in the C# version, we use a key-value dictionary to build a dictionary that
  has a property name as a key and a quoted function for selecting a property of a customer as a value,
  though we use F# type <code>Map</code>. We also don't have to write the type, because we use
  type annotation to specify that the argument of the function is of type <code>Customer</code>
  and everything else is inferred automatically by the F# compiler. In the <code>main</code> function
  we just ask user for a property and a value then we find the selector for the given property
  in the dictionary and finally we build the query using <code>queryTempalte</code>, execute it
  and print the results.</p>

<h2>Dynamic Queries #2: Combining Functions Using “OR” and “AND”</h2>
<p>In the next example, we will write a more complex application that generates a LINQ query 
  by combining several criteria using <code>AND</code> or <code>OR</code> logical operator. We will again
  use the <code>Customer</code> type from the Northwind database and we will allow selecting same properties
  as in the previous example. As mentioned in the previous article about the C# version, this allows the user 
  to enter queries like <code>(CompanyName = "Google") OR (Country = "Czech Republic")</code> or 
  <code>(CompanyName = "Microsoft") AND (Country = "USA")</code>. The way of combining the conditions is a bit
  limited, because you can use only a few properties and you can only test if it contains a string as a sub-string,
  an you also can't build a tree of logical operations, but you could easily extend the example to support
  all of these options as well. </p>
<p>As we use the same data context (<code>db</code>) and same dictionary with property name as a key
  and quoted function for selecting a property of the customer (<code>dict</code>), I won't repeat them in 
  this example and let's look at our code for combining conditions:</p>
<pre lang="fsharp">
// Elementary conditions
let falseCond = « fun (c:Customer) -&gt; false »
let trueCond  = « fun (c:Customer) -&gt; true  »
</pre>
<p>We first defined two elementary conditions, <code>falseCond</code> which returns <code>false</code> for
  every customer and <code>trueConde</code> which returns always <code>true</code>. The functions are quoted,
  so it will be possible to translate them to SQL. We didn't have to write the type explicitly, because
  the F# compiler infers them, but if you move a mouse pointer over these values, you'll see that the type is
  <code>Expr&lt;Customer -&gt; bool&gt;</code>, which is indeed a type of quotation (<code>Expr</code>)
  of a function taking <code>Customer</code> and returning a value of <code>bool</code>. The next
  step is to define <em>combinators</em> for combining quoted functions:</p>  
<pre lang="fsharp">
// Condition combinators, declared as infix operators
let (||*) f g = « fun (c:Customer) -&gt; (_ c) || (_ c) » f g
let (&amp;&amp;*) f g = « fun (c:Customer) -&gt; (_ c) &amp;&amp; (_ c) » f g
</pre>
<p>Because F# allows us to declare our own infix binary operators, we declare combinators as operators -
  you'll see later that this makes the code more readable. Both operators take two quoted functions
  (<code>f</code> and <code>g</code>) as the arguments and use them to build a quotation that represents
  a function that combines <code>f</code> and <code>g</code> using either <code>||</code> or <code>&amp;&amp;</code> operator.
  The type of these operator is slightly more complicated, because it takes two conditions (of type mentioned above)
  and returns a condition - <code>Expr&lt;Customer-&gt;bool&gt; -&gt; Expr&lt;Customer-&gt;bool&gt; -&gt; Expr&lt;Customer-&gt;bool&gt;</code>,
  but as usually in F#, we didn't have to write this type, because the compiler inferred it automatically.
  The following example demonstrates how we can use the combinators:</p>
<pre lang="fsharp">
/// Demonstrates the use of combinators
let simplecombine_demo() = 
  let isUk = « fun (c:Customer) -&gt; c.Country = "UK" »
  let isSeattle = « fun (c:Customer) -&gt; c.City = "Seattle" »

  // Build an expression using 'or' combinator
  let expr = isUk ||* isSeattle
  let q = custTable |&gt; « { for c in _ when _ c -&gt; c } » expr
  SQL q |&gt; dump
</pre>
<p>In this sample we build a query that returns customers who live in UK or in Seattle.
  To build the query we first write two basic quoted functions for testing the conditions and
  then combine them using the <code>||*</code> combinator. Finally, we use the created
  quotation as an argument to a quotation template to build a representation of full query, which
  is later executed using the <code>SQL</code> function.</p>

<p>Let's move to the final example where we'll combine conditions for all the properties 
  of a customer. We'll do this using a <code>Fold</code> method of the dictionary which takes an
  initial value and allows us to combine the initial value with every key-value pair in the dictionary.
  This means that we will for example start with <code>FALSE</code>, than combine it with a first condition
  and get something like <code>FALSE || (Country = "UK")</code> and so on.</p>
<pre lang="fsharp">
/// An advanced demo using 'fold' to combine more than 2 conditions
let combining_demo() = 
  Console.Write("Build || or &amp;&amp; query (enter 'or' or 'and'):\n&gt; ");
  let generateOr = Console.ReadLine().ToLower() = "or"  

  // Declare infix combinator, depending on an operator we use
  let (^^) = if generateOr then (||*) else (&amp;&amp;*)
  
  // Build the expression by 'folding' all items in dictionary
  let expr = 
    dict.Fold
      // Function to combine key, value &amp; state
      (fun key propSelector e -&gt;
        // Read the value from the user
        Console.Write("Enter value for '{0}':\n&gt; ", key);
        let enteredVal = Console.ReadLine();
        
        // Build a condition testing whether entered value is a substring
        // using 'testTemplate' and combine it with current expression using '^^'
        let currentExpr = 
          « fun (c:Customer) -&gt; ((_ c):string).IndexOf(_:string) &lt;&gt; -1 » 
            propSelector « §enteredVal »
        (currentExpr ^^ e)) 
      // Initial state
      (if generateOr then falseCond else trueCond)
  
  // Dump the results..  
  let q = custTable |&gt; « { for c in _ when _ c -&gt; c } » expr
  SQL q |&gt; dump
</pre>
<p>In the example we first ask user to choose if he wants to combine conditions using 
  <code>||</code> or using <code>&amp;&amp;</code> and declare a new operator <code>^^</code> (available
  just locally in the function) that represents the selected combinator. Then we call the <code>Fold</code>
  function, in which we ask the user for a value for every property, build a quotation 
  representing the current condition and combine the current condition with the condition
  that we generated so far (<code>e</code>). Also note that when combining conditions using
  <code>&amp;&amp;</code>, the initial condition is <code>trueCond</code> (returning always <code>true</code>) and for
  <code>||</code>, the initial condition always returns <code>false</code>.</p>

<h2>Summary</h2>
<p>When you compare the code I wrote earlier in C# 3.0 and the code presented in this article, you 
  can say that the difference is not that big. Actually, the most important differences in the code are that in 
  F#, code is quoted explicitly (which allows the compiler to infer the type automatically) and that
  F# supports quotation templates with holes that can be filled later. There are however a differences
  from an architectural point of view. In C# 3.0 it is more difficult to encapsulate constructs
  like elementary conditions or combinators, because these are values of some very complicated type that
  you'd probably have to expose as static class members. 
  On the other side, in F# the type doesn't have to be written explicitly (but you can still see it if you need it)
  and all the basic building blocks that we used in this code can be easily written as a members of a module
  that can be distributed for example as a DLL.
  </p>

<h2>Downloads &amp; References <a name="dynamicflinq"></a></h2>
<ul>
  <li><a href="/articles/dynamic-flinq/flinq_orcasb2.zip">Download FLinq for VS 2008 (beta 2)</a> (123 kB)</li>
  <li><a href="/articles/dynamic-flinq/sample.zip">Download the sample code for this article</a> (52 kB)</li>
</ul>

<ul>
  <li>[1] <a href="http://tomasp.net/blog/fswebtools-intro.aspx">F# Web Toolkit: "Ajax" applications made simple</a> [<a href="http://tomasp.net/blog/fswebtools-intro.aspx" target="_blank">^</a>] - Blog | TomasP.Net</li>
  <li>[2] <a href="http://tomasp.net/blog/dynamic-linq-queries.aspx">Building LINQ Queries at Runtime in C#</a> [<a href="http://tomasp.net/blog/dynamic-linq-queries.aspx" target="_blank">^</a>] - Blog | TomasP.Net</li>
</ul>